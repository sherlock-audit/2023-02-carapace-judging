rvierdiiev

medium

# ProtectionPool._accruePremiumAndExpireProtections will stop working when lendingPool will have big number of protections

## Summary
ProtectionPool._accruePremiumAndExpireProtections will stop working when lendingPool will have big number of protections
## Vulnerability Detail
ProtectionPool._accruePremiumAndExpireProtections function is called to collect premium for active protections and also remove expired protections from active list.

https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/pool/ProtectionPool.sol#L963-L1021
```solidity
  function _accruePremiumAndExpireProtections(
    LendingPoolDetail storage lendingPoolDetail,
    uint256 _lastPremiumAccrualTimestamp,
    uint256 _latestPaymentTimestamp
  )
    internal
    returns (
      uint256 _accruedPremiumForLendingPool,
      uint256 _totalProtectionRemoved
    )
  {
    /// Get all active protection indexes for the lending pool
    uint256[] memory _protectionIndexes = lendingPoolDetail
      .activeProtectionIndexes
      .values();


    /// Iterate through all active protection indexes for the lending pool
    uint256 _length = _protectionIndexes.length;
    for (uint256 j; j < _length; ) {
      uint256 _protectionIndex = _protectionIndexes[j];
      ProtectionInfo storage protectionInfo = protectionInfos[_protectionIndex];


      /// Verify & accrue premium for the protection and
      /// if the protection is expired, then mark it as expired
      (
        uint256 _accruedPremiumInUnderlying,
        bool _expired
      ) = ProtectionPoolHelper.verifyAndAccruePremium(
          poolInfo,
          protectionInfo,
          _lastPremiumAccrualTimestamp,
          _latestPaymentTimestamp
        );
      _accruedPremiumForLendingPool += _accruedPremiumInUnderlying;


      if (_expired) {
        /// Add removed protection amount to the total protection removed
        _totalProtectionRemoved += protectionInfo
          .purchaseParams
          .protectionAmount;


        ProtectionPoolHelper.expireProtection(
          protectionBuyerAccounts,
          protectionInfo,
          lendingPoolDetail,
          _protectionIndex
        );
        emit ProtectionExpired(
          protectionInfo.buyer,
          protectionInfo.purchaseParams.lendingPoolAddress,
          protectionInfo.purchaseParams.protectionAmount
        );
      }


      unchecked {
        ++j;
      }
    }
  }
```
This function iterates over `lendingPoolDetail.activeProtectionIndexes.values()`. This array includes all protections that are not expired yet. The problem here is that when new protection is created, length of array increases. So it's actually very likely that this array will be big enough to make function revert with out of gas error. 

In case if this will happen, then accruing premium for that lending pool will be blocked forever.
## Impact
Depositors will lose their premium.
## Code Snippet
Provided above
## Tool used

Manual Review

## Recommendation
Maybe you need to have limit of `lendingPoolDetail.activeProtectionIndexes` that can be open at same time.
You can control this [when adding new protection](https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/pool/ProtectionPool.sol#L886-L890).