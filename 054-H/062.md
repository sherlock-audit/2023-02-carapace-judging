Ruhum

medium

# User can game withdrawal requests to be able to withdraw their whole balance in every cycle

## Summary
There's no penalty for not fulfilling a withdrawal request. A user can create a request for every cycle so that they can withdraw their funds whenever they want.

## Vulnerability Detail
The withdrawal request system is supposed to limit the time in which a user can withdraw their funds (open period after the next cycle). A user is able to create a withdrawal request for every cycle so that they can withdraw whenever they want. There's no penalty for that. 

## Impact
It allows users to bypass the timelock where they would have to wait for the current and next cycle to pass.

## Code Snippet
At the start of every cycle, the user has to call `requestWithdrawal()`. Given that they start in cycle X, they will be able to withdraw their funds from cycle X+2 onwards at any time they want.

In `_requestWithdrawal()` the contract verifies only a single thing: does the user have enough STokens to cover the withdrawal request: https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/pool/ProtectionPool.sol#L1061-L1097
```sol
  function _requestWithdrawal(uint256 _sTokenAmount) internal {
    uint256 _sTokenBalance = balanceOf(msg.sender);
    if (_sTokenAmount > _sTokenBalance) {
      revert InsufficientSTokenBalance(msg.sender, _sTokenBalance);
    }

    /// Get current cycle index for this pool
    uint256 _currentCycleIndex = poolCycleManager.getCurrentCycleIndex(
      address(this)
    );

    /// Actual withdrawal is allowed in open period of cycle after next cycle
    /// For example: if request is made in at some time in cycle 1,
    /// then withdrawal is allowed in open period of cycle 3
    uint256 _withdrawalCycleIndex = _currentCycleIndex + 2;

    WithdrawalCycleDetail storage withdrawalCycle = withdrawalCycleDetails[
      _withdrawalCycleIndex
    ];

    /// Cache existing requested amount for the cycle for the sender
    uint256 _oldRequestAmount = withdrawalCycle.withdrawalRequests[msg.sender];
    withdrawalCycle.withdrawalRequests[msg.sender] = _sTokenAmount;

    unchecked {
      /// Update total requested withdrawal amount for the cycle considering existing requested amount
      if (_oldRequestAmount > _sTokenAmount) {
        withdrawalCycle.totalSTokenRequested -= (_oldRequestAmount -
          _sTokenAmount);
      } else {
        withdrawalCycle.totalSTokenRequested += (_sTokenAmount -
          _oldRequestAmount);
      }
    }

    emit WithdrawalRequested(msg.sender, _sTokenAmount, _withdrawalCycleIndex);
  }
```
None of the tokens are locked up or anything like that. The withdrawal request has no effect on any of the other properties of the contract. In the open period after the next cycle, the user can redeem their withdrawal request. But, they don't have to.

## Tool used

Manual Review

## Recommendation
If you want to limit the timing of the user's withdrawal, you have to prevent them from requesting a withdrawal in every cycle.

For example, when a user requests a withdrawal you burn their SToken shares. After the next cycle passed, you allow the user to withdraw the underlying tokens. That way, they can only initiate a single withdrawal request. They have to commit to the withdrawal.
