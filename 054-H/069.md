rvierdiiev

high

# User can create withdrawal request for 2 cycles in a row in order to decrease withdraw time from 2 cycles to only 1 or even to 0

## Summary
User can create withdrawal request for 2 cycles in a row in order to decrease withdraw time from 2 cycles to only 1 or even to 0.
## Vulnerability Detail
If protection seller wants to withdraw deposited funds he need to create request for withdrawal. If you do request in cycle n, then you will be able to withdraw that amount in n+2 cycle when state is open. This is done in order to make all depositors have same risk of default and loosing of funds.
Let's look at `ProtectionPool._requestWithdrawal`
https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/pool/ProtectionPool.sol#L1061-L1097
```solidity
  function _requestWithdrawal(uint256 _sTokenAmount) internal {
    uint256 _sTokenBalance = balanceOf(msg.sender);
    if (_sTokenAmount > _sTokenBalance) {
      revert InsufficientSTokenBalance(msg.sender, _sTokenBalance);
    }


    /// Get current cycle index for this pool
    uint256 _currentCycleIndex = poolCycleManager.getCurrentCycleIndex(
      address(this)
    );


    /// Actual withdrawal is allowed in open period of cycle after next cycle
    /// For example: if request is made in at some time in cycle 1,
    /// then withdrawal is allowed in open period of cycle 3
    uint256 _withdrawalCycleIndex = _currentCycleIndex + 2;


    WithdrawalCycleDetail storage withdrawalCycle = withdrawalCycleDetails[
      _withdrawalCycleIndex
    ];


    /// Cache existing requested amount for the cycle for the sender
    uint256 _oldRequestAmount = withdrawalCycle.withdrawalRequests[msg.sender];
    withdrawalCycle.withdrawalRequests[msg.sender] = _sTokenAmount;


    unchecked {
      /// Update total requested withdrawal amount for the cycle considering existing requested amount
      if (_oldRequestAmount > _sTokenAmount) {
        withdrawalCycle.totalSTokenRequested -= (_oldRequestAmount -
          _sTokenAmount);
      } else {
        withdrawalCycle.totalSTokenRequested += (_sTokenAmount -
          _oldRequestAmount);
      }
    }


    emit WithdrawalRequested(msg.sender, _sTokenAmount, _withdrawalCycleIndex);
  }
```
This function creates `withdrawalRequests` for msg.sender and allows him to request not bigger than token balance he has.
The request is created for current cycle + 2 period.

Later, in `withdraw` function created request for current cycle [will be fetched](https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/pool/ProtectionPool.sol#L237-L242). 

This is how this can be used by depositor in order to make all his next deposits will be able to withdraw in 1 cycle, not in 2.
1.At cycle n user deposits some amount.
2.At cycle n he makes withdrawal request to withdraw all amount. So now he [has request for n+2 cycle](https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/pool/ProtectionPool.sol#L1083).
3.At cycle n+1 he makes withdrawal request to withdraw all amount again. So now he has request for n+3 cycle.
4.At cycle n+2 he withdraws first deposit. request for n+2 cycle is [cleared](https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/pool/ProtectionPool.sol#L265).
5.Now he can deposit at cycle n+2 and will be able to withdraw at next cycle n+3 as he still has request or he can also wait till n+3 cycle to deposit at that time and be able to withdraw at same cycle.
## Impact
User doesn't share risks with other depositors and receive premium for free.
## Code Snippet
Provided above
## Tool used

Manual Review

## Recommendation
When user creates new withdraw request, then total amount of his requests(n+1 and n+2) should not be bigger than his sToken balance.