rvierdiiev

high

# Attacker can withdraw in same or next epoch when he deposited

## Summary
Attacker can withdraw in same or next epoch when he deposited
## Vulnerability Detail
If protection seller wants to withdraw deposited funds he need to create request for withdrawal. If you do request in cycle n, then you will be able to withdraw that amount in n+2 cycle when state is open. This is done in order to make all depositors have same risk of default and loosing of funds.
Let's look at `ProtectionPool._requestWithdrawal`
https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/pool/ProtectionPool.sol#L1061-L1097
```solidity
  function _requestWithdrawal(uint256 _sTokenAmount) internal {
    uint256 _sTokenBalance = balanceOf(msg.sender);
    if (_sTokenAmount > _sTokenBalance) {
      revert InsufficientSTokenBalance(msg.sender, _sTokenBalance);
    }


    /// Get current cycle index for this pool
    uint256 _currentCycleIndex = poolCycleManager.getCurrentCycleIndex(
      address(this)
    );


    /// Actual withdrawal is allowed in open period of cycle after next cycle
    /// For example: if request is made in at some time in cycle 1,
    /// then withdrawal is allowed in open period of cycle 3
    uint256 _withdrawalCycleIndex = _currentCycleIndex + 2;


    WithdrawalCycleDetail storage withdrawalCycle = withdrawalCycleDetails[
      _withdrawalCycleIndex
    ];


    /// Cache existing requested amount for the cycle for the sender
    uint256 _oldRequestAmount = withdrawalCycle.withdrawalRequests[msg.sender];
    withdrawalCycle.withdrawalRequests[msg.sender] = _sTokenAmount;


    unchecked {
      /// Update total requested withdrawal amount for the cycle considering existing requested amount
      if (_oldRequestAmount > _sTokenAmount) {
        withdrawalCycle.totalSTokenRequested -= (_oldRequestAmount -
          _sTokenAmount);
      } else {
        withdrawalCycle.totalSTokenRequested += (_sTokenAmount -
          _oldRequestAmount);
      }
    }


    emit WithdrawalRequested(msg.sender, _sTokenAmount, _withdrawalCycleIndex);
  }
```
This function creates `withdrawalRequests` for msg.sender and allows him to request not bigger than token balance he has.
The request is created for current cycle + 2 period.

Later, in `withdraw` function created request for current cycle [will be fetched](https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/pool/ProtectionPool.sol#L237-L242). 

In case if user transfers his tokens, all created request are still in place for him.
This is how it can be used by attacker in order to avoid depositing for at least 2 periods.

1.Attacker buys sTokens on market at cycle n.
2.Attacker creates withdraw request for cycle n+2.
3.Attacker sells sTokens so now he don't have any tokens, but he has withdraw request for himself.
4.At the beginning of n+2 cycle attacker deposits some assets(amount of sTokens is same as withdrawal request). Also he already has withdrawal request for that cycle.
5.When payment is done, attacker withdraws and receives premium for free.
6.He can repeat that as many times as he wishes.
## Impact
Attacker doesn't share risk with other stakers and receives premium for free.
## Code Snippet
Provided above
## Tool used

Manual Review

## Recommendation
I think that when transferring tokens you need update withdrawal request for `from` account.
Withdrawal request of from account should not be bigger than his balance.