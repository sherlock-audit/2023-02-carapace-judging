sorrynotsorry

high

# Users can claim their funds from the pools which are in Late state without their capitals being locked.

## Summary
Users can claim their funds from the pools which are in Late state without their capitals being locked.

## Vulnerability Detail
`DefaultStateManager`'s internal function `_assessState` assesses the state of a given protection pool and updates state changes & initiate related actions as needed.
[NATSPEC](https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/DefaultStateManager.sol#L285-L288)

So it's the main function that is responsible for pool state changes and calling `_moveFromActiveToLockedState` internally to move the lending pool from active state to locked state (meaning that the capital is locked in the protection pool.) via `ProtectionPool.lockCapital`.

And `_assessState` is being called on these conditions;
1. During registering a pool via `DefaultStateManager::registerProtectionPool` function,
2. To assess all pool states via `DefaultStateManager::assessStates` function,
3. And to individually assess the given pools via `DefaultStateManager::assessStateBatch`

However, if any pool is late and since the pool status is not changed from Active or LateWithinGracePeriod -> Late without calling `_assessState` , it's possible that the users can still claim their capital via `ProtectionPool`'s `claimUnlockedCapital`.

And `_moveFromActiveToLockedState` is only called by  `_assessState` and the system relies on the CronJob to assess the states which might fail as there is no guarantee of a possible downtime.


## Impact
Bricking the main functionality of the project, loss of user funds due to rate changes, the system is open to manipulation

## Code Snippet
```solidity
  function _assessState(ProtectionPoolState storage poolState) internal {
    poolState.updatedTimestamp = block.timestamp;


    /// assess the state of all reference lending pools of this protection pool
    (
      address[] memory _lendingPools,
      LendingPoolStatus[] memory _currentStatuses
    ) = poolState
        .protectionPool
        .getPoolInfo()
        .referenceLendingPools
        .assessState();

    /// Compare previous and current status of each lending pool and perform the required state transition
    uint256 _length = _lendingPools.length;
    for (uint256 _lendingPoolIndex; _lendingPoolIndex < _length; ) {
      /// Get the lending pool state details
      address _lendingPool = _lendingPools[_lendingPoolIndex];
      LendingPoolStatusDetail storage lendingPoolStateDetail = poolState
        .lendingPoolStateDetails[_lendingPool];

      /// Get the previous and current status of the lending pool
      LendingPoolStatus _previousStatus = lendingPoolStateDetail.currentStatus;
      LendingPoolStatus _currentStatus = _currentStatuses[_lendingPoolIndex];

      if (_previousStatus != _currentStatus) {
        console.log(
          "DefaultStateManager: Lending pool %s status is changed from %s to  %s",
          _lendingPool,
          uint256(_previousStatus),
          uint256(_currentStatus)
        );
      }

      /// State transition 1: Active or LateWithinGracePeriod -> Late
      if (
        (_previousStatus == LendingPoolStatus.Active ||
          _previousStatus == LendingPoolStatus.LateWithinGracePeriod) &&
        _currentStatus == LendingPoolStatus.Late
      ) {
        /// Update the current status of the lending pool to Late
        /// and move the lending pool to the locked state
        lendingPoolStateDetail.currentStatus = LendingPoolStatus.Late;
        _moveFromActiveToLockedState(poolState, _lendingPool);

        /// Capture the timestamp when the lending pool became late
        lendingPoolStateDetail.lateTimestamp = block.timestamp;
      } else if (_previousStatus == LendingPoolStatus.Late) {
        /// Once there is a late payment, we wait for 2 payment periods.
        /// After 2 payment periods are elapsed, either full payment is going to be made or not.
        /// If all missed payments(full payment) are made, then a pool goes back to active.
        /// If full payment is not made, then this lending pool is in the default state.
        if (
          block.timestamp >
          (lendingPoolStateDetail.lateTimestamp +
            _getTwoPaymentPeriodsInSeconds(poolState, _lendingPool))
        ) {
          /// State transition 2: Late -> Active
          if (_currentStatus == LendingPoolStatus.Active) {
            /// Update the current status of the lending pool to Active
            /// and move the lending pool to the active state
            lendingPoolStateDetail.currentStatus = LendingPoolStatus.Active;
            _moveFromLockedToActiveState(poolState, _lendingPool);

            /// Clear the late timestamp
            lendingPoolStateDetail.lateTimestamp = 0;
          }
          /// State transition 3: Late -> Defaulted
          else if (_currentStatus == LendingPoolStatus.Late) {
            /// Update the current status of the lending pool to Active
            lendingPoolStateDetail.currentStatus = LendingPoolStatus.Defaulted;

            // Default state transition will be implemented in the next version of the protocol
            // _moveFromLockedToDefaultedState(poolState, _lendingPool);
          }
        }
      } else if (
        _previousStatus == LendingPoolStatus.Defaulted ||
        _previousStatus == LendingPoolStatus.Expired
      ) {
        /// no state transition for Defaulted or Expired state
      } else {
        /// Only update the status in storage if it is changed
        if (_previousStatus != _currentStatus) {
          lendingPoolStateDetail.currentStatus = _currentStatus;
          /// No action required for any other state transition
        }
      }

      unchecked {
        ++_lendingPoolIndex;
      }
    }
  }
```

```solidity
  function _moveFromActiveToLockedState(
    ProtectionPoolState storage poolState,
    address _lendingPool
  ) internal {
    IProtectionPool _protectionPool = poolState.protectionPool;

    /// step 1: calculate & lock the capital amount in the protection pool
    (uint256 _lockedCapital, uint256 _snapshotId) = _protectionPool.lockCapital(
      _lendingPool
    );

    /// step 2: create and store an instance of locked capital for the lending pool
    poolState.lockedCapitals[_lendingPool].push(
      LockedCapital({
        snapshotId: _snapshotId,
        amount: _lockedCapital,
        locked: true
      })
    );

    emit LendingPoolLocked(
      _lendingPool,
      address(_protectionPool),
      _snapshotId,
      _lockedCapital
    );
  }
```

## Tool used

Manual Review

## Recommendation
The team might consider calling `_assessState` during claims.
