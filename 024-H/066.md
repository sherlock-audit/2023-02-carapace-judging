Koolex

high

# Calculating the claimable amount across all locked capitals for a given seller and a lending pool returns only the amount of the last unlocked capital

## Summary
Calculating the claimable amount across all locked capitals for a given seller and a lending pool returns only the amount of the last unlocked capital 

## Vulnerability Detail
When the seller calls `ProtectionPool.claimUnlockedCapital` function to claim unlocked capitals, It calls `defaultStateManager.calculateAndClaimUnlockedCapital` to calculate the claimable amount, then it transfers the amount to the seller if it is greater than zero.

`defaultStateManager.calculateAndClaimUnlockedCapital` function works as follows:
1. Iterates over all lending pools.
2. For each lending pool calculates the claimable amount for the seller.
3. Updates the last claimed snapshot id for the seller.

At **point 2**, it calls the internal function `_calculateClaimableAmount` to return the total unlocked capitals per lending pool.

However, looking at the code of `_calculateClaimableAmount`, it always returns the calculated claimable amount of the last unclaimed snapshot (if there is at least one unclaimed snapshot). 

```sh
 /// The claimable amount for the given seller is proportional to the seller's share of the total supply at the snapshot
        /// claimable amount = (seller's snapshot balance / total supply at snapshot) * locked capital amount
        _claimableUnlockedCapital =
          (_poolSToken.balanceOfAt(_seller, _snapshotId) *
            lockedCapital.amount) /
          _poolSToken.totalSupplyAt(_snapshotId);

        /// Update the last claimed snapshot id for the seller
        _latestClaimedSnapshotId = _snapshotId;
```

This means if the seller has two or more claimable unlocked capitals, then s/he will only receive the calculated amount of the last snapshot taken.

If you check the code at `defaultStateManager.calculateAndClaimUnlockedCapital` function, there is a comment describing what is the expected result of `_calculateClaimableAmount` .

```sh
      /// Calculate the claimable amount across all the locked capital instances for a given protection pool
      (
        uint256 _unlockedCapitalPerLendingPool,
        uint256 _snapshotId
      ) = _calculateClaimableAmount(poolState, _lendingPool, _seller);
      _claimedUnlockedCapital += _unlockedCapitalPerLendingPool;
```

As you notice, it expects to calculate the claimable amount across all locked capitals and not only the last one.

*Note: in case this is done on purpose (for whatever reason), the unfairness issue still exists as investors will get less share if they don't claim on time (e.g. before another snapshot is taken and gets unlocked) unlike others who always calim whenever a new unlocked capital is claimable.*  
## Impact
- The investor/seller receives his/her share of the last snapshot only in case s/he has two or more claimable unlocked capitals which is unfair.
- All other snapshots except the last one will be marked as claimed although the relevant amount was not transferred to the investor.

## Code Snippet

- ProtectionPool.claimUnlockedCapital
```sh
    /// Investors can claim their total share of released/unlocked capital across all lending pools
    uint256 _claimableAmount = defaultStateManager
      .calculateAndClaimUnlockedCapital(msg.sender);

    if (_claimableAmount > 0) {
      console.log(
        "Total sToken underlying: %s, claimableAmount: %s",
        totalSTokenUnderlying,
        _claimableAmount
      );
      /// transfer the share of unlocked capital to the receiver
      poolInfo.underlyingToken.safeTransfer(_receiver, _claimableAmount);
    }
```
https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/pool/ProtectionPool.sol#L427-L445


- defaultStateManager.calculateAndClaimUnlockedCapital

```sh
 /// Calculate the claimable amount across all the locked capital instances for a given protection pool
      (
        uint256 _unlockedCapitalPerLendingPool,
        uint256 _snapshotId
      ) = _calculateClaimableAmount(poolState, _lendingPool, _seller);
      _claimedUnlockedCapital += _unlockedCapitalPerLendingPool;

      /// update the last claimed snapshot id for the seller for the given lending pool,
      /// so that the next time the seller claims, the calculation starts from the last claimed snapshot id
      poolState.lastClaimedSnapshotIds[_lendingPool][_seller] = _snapshotId;

```
https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/DefaultStateManager.sol#L186-L195


- defaultStateManager._calculateClaimableAmount
```sh
      /// Verify that the seller does not claim the same snapshot twice
      if (!lockedCapital.locked && _snapshotId > _lastClaimedSnapshotId) {
        ERC20SnapshotUpgradeable _poolSToken = ERC20SnapshotUpgradeable(
          address(poolState.protectionPool)
        );

        console.log(
          "balance of seller: %s, total supply: %s at snapshot: %s",
          _poolSToken.balanceOfAt(_seller, _snapshotId),
          _poolSToken.totalSupplyAt(_snapshotId),
          _snapshotId
        );

        /// The claimable amount for the given seller is proportional to the seller's share of the total supply at the snapshot
        /// claimable amount = (seller's snapshot balance / total supply at snapshot) * locked capital amount
        _claimableUnlockedCapital =
          (_poolSToken.balanceOfAt(_seller, _snapshotId) *
            lockedCapital.amount) /
          _poolSToken.totalSupplyAt(_snapshotId);

        /// Update the last claimed snapshot id for the seller
        _latestClaimedSnapshotId = _snapshotId;

        console.log(
          "Claimable amount for seller %s is %s",
          _seller,
          _claimableUnlockedCapital
        );
      }

```
https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/DefaultStateManager.sol#L487-L515


## Tool used

Manual Review

## Recommendation
Just replace **=** with **+=** to get the sum of all amounts

For example:

```sh 
        _claimableUnlockedCapital +=
          (_poolSToken.balanceOfAt(_seller, _snapshotId) *
            lockedCapital.amount) /
          _poolSToken.totalSupplyAt(_snapshotId);
```  