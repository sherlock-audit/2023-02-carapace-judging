XKET

high

# `DefaultStateManager._calculateClaimableAmount()` calculates the claimable unlocked amount wrongly.



## Summary
`DefaultStateManager._calculateClaimableAmount()` calculates the claimable unlocked amount across the locked capital instances.

But it returns the last claimable amount for the last unlocked capital rather than adding all available unlocked amounts.

## Vulnerability Detail
`DefaultStateManager._calculateClaimableAmount()` iterate over the locked capitals and calculate the claimable amount like below.

```solidity
    /// Iterate over the locked capital instances and calculate the claimable amount
    uint256 _length = lockedCapitals.length;
    for (uint256 _index = 0; _index < _length; ) {
      LockedCapital storage lockedCapital = lockedCapitals[_index];
      uint256 _snapshotId = lockedCapital.snapshotId;

      console.log(
        "lockedCapital.locked: %s, amt: %s",
        lockedCapital.locked,
        lockedCapital.amount
      );

      /// Verify that the seller does not claim the same snapshot twice
      if (!lockedCapital.locked && _snapshotId > _lastClaimedSnapshotId) {
        ERC20SnapshotUpgradeable _poolSToken = ERC20SnapshotUpgradeable(
          address(poolState.protectionPool)
        );

        console.log(
          "balance of seller: %s, total supply: %s at snapshot: %s",
          _poolSToken.balanceOfAt(_seller, _snapshotId),
          _poolSToken.totalSupplyAt(_snapshotId),
          _snapshotId
        );

        /// The claimable amount for the given seller is proportional to the seller's share of the total supply at the snapshot
        /// claimable amount = (seller's snapshot balance / total supply at snapshot) * locked capital amount
        _claimableUnlockedCapital = //@audit should be +=
          (_poolSToken.balanceOfAt(_seller, _snapshotId) *
            lockedCapital.amount) /
          _poolSToken.totalSupplyAt(_snapshotId);

        /// Update the last claimed snapshot id for the seller
        _latestClaimedSnapshotId = _snapshotId;

        console.log(
          "Claimable amount for seller %s is %s",
          _seller,
          _claimableUnlockedCapital
        );
      }

      unchecked {
        ++_index;
      }
    }
```

But it returns the last claimable amount without adding all and users might lose their underlying like below.

1. Alice deposited some underlying to the protection pool and became a seller.
2. The first protection(amount = 100) was locked and unlocked after 2 * `PaymentPeriods`.
3. But Alice couldn't claim her unlocked amount as she was offline.
4. After that, the second protetion(amount = 100) was locked and unlocked again.
5. Then Alice has a right to claim 200 in total but she can claim for the second one only for now.

## Impact
Sellers would receive smaller underlying amounts from unlocked capitals than they can.

## Code Snippet
https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/DefaultStateManager.sol#L502

## Tool used
Manual Review

## Recommendation
`DefaultStateManager._calculateClaimableAmount()` should sum all claimable unlocked amounts.

```solidity
File: DefaultStateManager.sol
502:         _claimableUnlockedCapital += 
503:           (_poolSToken.balanceOfAt(_seller, _snapshotId) *
504:             lockedCapital.amount) /
505:           _poolSToken.totalSupplyAt(_snapshotId);
```