bin2chen

high

# claimUnlockedCapital() can be repeatedly claimed

## Summary
claimUnlockedCapital() can be repeatedly claimed , Malicious seller can steal fund
## Vulnerability Detail

When the locked capital is unlocked, the seller can retrieve the capital via ProtectionPool.claimUnlockedCapital().
The current implementation loops through the lockedCapitals[], accumulating all amounts that are unlocked and have a SnapshotId greater than the _lastClaimedSnapshotId
Finally update poolState.lastClaimedSnapshotIds[_lendingPool][_seller]  = last _snapshotId to prevent duplicate claim

 The code is implemented as follows:

```solidity
  function calculateAndClaimUnlockedCapital(address _seller)
    external
    override
    returns (uint256 _claimedUnlockedCapital)
  {
...
    for (uint256 _lendingPoolIndex; _lendingPoolIndex < _length; ) {
      address _lendingPool = _lendingPools[_lendingPoolIndex];

      (
        uint256 _unlockedCapitalPerLendingPool,
        uint256 _snapshotId
      ) = _calculateClaimableAmount(poolState, _lendingPool, _seller);
      _claimedUnlockedCapital += _unlockedCapitalPerLendingPool;

      /// update the last claimed snapshot id for the seller for the given lending pool,
      /// so that the next time the seller claims, the calculation starts from the last claimed snapshot id
      poolState.lastClaimedSnapshotIds[_lendingPool][_seller] = _snapshotId;  //<---------------update to last claim _snapshotId 
```

```solidity
  function _calculateClaimableAmount(
    ProtectionPoolState storage poolState,
    address _lendingPool,
    address _seller
  )
    internal
    view
    returns (
      uint256 _claimableUnlockedCapital,
      uint256 _latestClaimedSnapshotId
    )
  {
    /// Retrieve the last claimed snapshot id for the seller from storage
    uint256 _lastClaimedSnapshotId = poolState.lastClaimedSnapshotIds[
      _lendingPool
    ][_seller];

     //@audit<--------------Missing first set _latestClaimedSnapshotId=_lastClaimedSnapshotId, if the following loops are all not satisfied, it will result in the return of _latestClaimedSnapshotId == 0

    /// Retrieve the locked capital instances for the given lending pool
    LockedCapital[] storage lockedCapitals = poolState.lockedCapitals[
      _lendingPool
    ];

    /// Iterate over the locked capital instances and calculate the claimable amount
    uint256 _length = lockedCapitals.length;
    for (uint256 _index = 0; _index < _length; ) {
      LockedCapital storage lockedCapital = lockedCapitals[_index];
      uint256 _snapshotId = lockedCapital.snapshotId;

      console.log(
        "lockedCapital.locked: %s, amt: %s",
        lockedCapital.locked,
        lockedCapital.amount
      );

      /// Verify that the seller does not claim the same snapshot twice
      if (!lockedCapital.locked && _snapshotId > _lastClaimedSnapshotId) {
        ERC20SnapshotUpgradeable _poolSToken = ERC20SnapshotUpgradeable(
          address(poolState.protectionPool)
        );

        console.log(
          "balance of seller: %s, total supply: %s at snapshot: %s",
          _poolSToken.balanceOfAt(_seller, _snapshotId),
          _poolSToken.totalSupplyAt(_snapshotId),
          _snapshotId
        );

        /// The claimable amount for the given seller is proportional to the seller's share of the total supply at the snapshot
        /// claimable amount = (seller's snapshot balance / total supply at snapshot) * locked capital amount
        _claimableUnlockedCapital =
          (_poolSToken.balanceOfAt(_seller, _snapshotId) *
            lockedCapital.amount) /
          _poolSToken.totalSupplyAt(_snapshotId);
          

        /// Update the last claimed snapshot id for the seller
        _latestClaimedSnapshotId = _snapshotId;

        console.log(
          "Claimable amount for seller %s is %s",
          _seller,
          _claimableUnlockedCapital
        );
      }

      unchecked {
        ++_index;
      }
    }
```
_calculateClaimableAmount() There is a problem with the implementation, that is: it forgot to set _latestClaimedSnapshotId=_lastClaimedSnapshotId first,
If the later loop all does not meet the conditions, will lead to the function returns _latestClaimedSnapshotId==0, and then reset poolState.lastClaimedSnapshotIds[_lendingPool][_seller] =0 in calculateAndClaimUnlockedCapital(), which can repeat claim

Exampleï¼š
lockedCapitals[0]={locked=false,amount=1000,snapshotId=1}
alice = 10%   and lastClaimedSnapshotIds[alice] =0

1.alice call calculateAndClaimUnlockedCapital():
get amount = 1000 * 10% = 100
lastClaimedSnapshotIds[alice]  = 1

2.alice call calculateAndClaimUnlockedCapital() again:
get amount = 0
lastClaimedSnapshotIds[alice]  = 0    (Due to the bug above, the loop is not satisfied and will be reset == 0)

3.alice call calculateAndClaimUnlockedCapital() again:
get amount = 1000 * 10% = 100
lastClaimedSnapshotIds[alice]  = 1

4. Repeat steps 2,3, until the contract balance is insufficient

## Impact

Malicious seller can steal fund by claimUnlockedCapital()

## Code Snippet

https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/DefaultStateManager.sol#L453-L521

https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/DefaultStateManager.sol#L195



## Tool used

Manual Review

## Recommendation
```solidity
  function _calculateClaimableAmount(
    ProtectionPoolState storage poolState,
    address _lendingPool,
    address _seller
  )
    internal
    view
    returns (
      uint256 _claimableUnlockedCapital,
      uint256 _latestClaimedSnapshotId
    )
  {
    /// Retrieve the last claimed snapshot id for the seller from storage
    uint256 _lastClaimedSnapshotId = poolState.lastClaimedSnapshotIds[
      _lendingPool
    ][_seller];

+    _latestClaimedSnapshotId = _lastClaimedSnapshotId;
```
