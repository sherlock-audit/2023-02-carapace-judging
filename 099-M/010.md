Bauer

medium

# Missing checks for amount 0  when buy or renew protection resulting in a large amount of garbage data  generated in the protocol

## Summary
There is no verification that  if the ```_protectionPurchaseParams.protectionAmount```  is 0 in the validation process when user buy or renew protection,  user can buy a large amount of protection to fill the array with ```_protectionPurchaseParams.protectionAmount = 0```. A large amount of garbage data generated in the protocol. It can causes the protocol to run slowly, possibly revert due to out of 

## Vulnerability Detail
```solidity
 function _verifyAndCreateProtection(
    uint256 _protectionStartTimestamp,
    ProtectionPurchaseParams calldata _protectionPurchaseParams,
    uint256 _maxPremiumAmount,
    bool _isRenewal
  ) internal {
    /// Verify that user can buy protection
    ProtectionPoolHelper.verifyProtection(
      poolCycleManager,
      defaultStateManager,
      address(this),
      poolInfo,
      _protectionStartTimestamp,
      _protectionPurchaseParams,
      _isRenewal
    );

    /// Step 1: Calculate & check the leverage ratio
    /// Ensure that leverage ratio floor is never breached
    totalProtection += _protectionPurchaseParams.protectionAmount;
    uint256 _leverageRatio = calculateLeverageRatio();
    if (_leverageRatio < poolInfo.params.leverageRatioFloor) {
      revert ProtectionPoolLeverageRatioTooLow(_leverageRatio);
    }

    /// Retrieve the lending pool detail and
    /// update the total protection of the lending pool by the protection amount
    LendingPoolDetail storage lendingPoolDetail = lendingPoolDetails[
      _protectionPurchaseParams.lendingPoolAddress
    ];
    lendingPoolDetail.totalProtection += _protectionPurchaseParams
      .protectionAmount;

    /// Step 2: Calculate the protection premium amount scaled to 18 decimals and
    /// scale it down to the underlying token decimals.
    (
      uint256 _premiumAmountIn18Decimals,
      uint256 _premiumAmount,
      bool _isMinPremium
    ) = ProtectionPoolHelper.calculateAndTrackPremium(
        premiumCalculator,
        protectionBuyerAccounts,
        poolInfo,
        lendingPoolDetail,
        _protectionPurchaseParams,
        _maxPremiumAmount,
        totalSTokenUnderlying,
        _leverageRatio
      );
    totalPremium += _premiumAmount;

    /// Step 3: Calculate protection in days and scale it to 18 decimals.
    uint256 _protectionDurationInDaysScaled = ((
      _protectionPurchaseParams.protectionDurationInSeconds
    ) * Constants.SCALE_18_DECIMALS) / uint256(Constants.SECONDS_IN_DAY);

    console.log(
      "protectionDurationInDays: %s, protectionPremium: %s, leverageRatio: ",
      _protectionDurationInDaysScaled,
      _premiumAmount,
      _leverageRatio
    );

    /// Step 4: Capture loan protection data for premium accrual calculation
    // solhint-disable-next-line
    (int256 _k, int256 _lambda) = AccruedPremiumCalculator.calculateKAndLambda(
      _premiumAmountIn18Decimals,
      _protectionDurationInDaysScaled,
      _leverageRatio,
      poolInfo.params.leverageRatioFloor,
      poolInfo.params.leverageRatioCeiling,
      poolInfo.params.leverageRatioBuffer,
      poolInfo.params.curvature,
      _isMinPremium ? poolInfo.params.minCarapaceRiskPremiumPercent : 0
    );

    /// Step 5: Add protection to the pool
    protectionInfos.push(
      ProtectionInfo({
        buyer: msg.sender,
        protectionPremium: _premiumAmount,
        startTimestamp: _protectionStartTimestamp,
        K: _k,
        lambda: _lambda,
        expired: false,
        purchaseParams: _protectionPurchaseParams
      })
    );

    /// Step 6: Track all loan protections for a lending pool to calculate
    // the total locked amount for the lending pool, when/if pool is late for payment
    uint256 _protectionIndex = protectionInfos.length - 1;
    lendingPoolDetail.activeProtectionIndexes.add(_protectionIndex);
    protectionBuyerAccounts[msg.sender].activeProtectionIndexes.add(
      _protectionIndex
    );

    emit ProtectionBought(
      msg.sender,
      _protectionPurchaseParams.lendingPoolAddress,
      _protectionPurchaseParams.protectionAmount,
      _premiumAmount
    );

    /// Step 7: transfer premium amount from buyer to pool
    poolInfo.underlyingToken.safeTransferFrom(
      msg.sender,
      address(this),
      _premiumAmount
    );
  }
```
Function ```buyProtection()``` or ```renewProtection()``` is used to buy or renew protection for users. The protocol will add protection information to the array. However,there is no verification that  if the ```_protectionPurchaseParams.protectionAmount```  is 0 in the validation process,  user can buy a large amount of protection to fill the array with ```_protectionPurchaseParams.protectionAmount = 0```. A large amount of garbage data generated in the protocol. It can causes the protocol to run slowly, possibly revert due to out of gas in the next process.

## Impact

Causes the protocol to run slowly, possibly revert due to out of gas.

## Code Snippet
https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/pool/ProtectionPool.sol#L795-L905
## Tool used

Manual Review

## Recommendation
```solidity
  function verifyProtection(
    IProtectionPoolCycleManager poolCycleManager,
    IDefaultStateManager defaultStateManager,
    address _protectionPool,
    ProtectionPoolInfo storage poolInfo,
    uint256 _protectionStartTimestamp,
    ProtectionPurchaseParams calldata _protectionPurchaseParams,
    bool _isRenewal
  ) external {
    require(_protectionPurchaseParams.protectionAmount != 0,"ZERO AMOUNT");
    /// Verify that the pool is not in OpenToSellers phase
    if (poolInfo.currentPhase == ProtectionPoolPhase.OpenToSellers) {
      revert IProtectionPool.ProtectionPoolInOpenToSellersPhase();
    }
```

