chaduke

medium

# A lender can bypass the protection amount limit check by buying more protections.

## Summary
When a lender buys a protection agreement, there is limit check: "that protection amount is less than or equal to the remaining principal  that buyer has lent to the underlying lending pool". However, this check can be bypassed by buying more protections for the same position for a given lending pool. As a result, the lender effectively bypass such a check.

## Vulnerability Detail
We show how a lender can bypass the protection amount limit check by buy more protections: 

1) First, when a lender calls ``buyProtection()``, ``buyProtection()`` will call ``canBuyProtection()`` (indirectly) to make sure that the protection amount is no more than the remaining principal  that buyer has lent to the underlying lending pool (see L162-L167):

[https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/pool/ReferenceLendingPools.sol#L132-L168](https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/pool/ReferenceLendingPools.sol#L132-L168)

2) However, such a limit can be bypassed if the buyer buys multiple protections for the same position for a given lending protocol (``LendingPoolAddress`` and ``nftLpTokenId``). There is no check in the whole protection buying process to check whether a protection is already in place for the given position. 

```javascript
function buyProtection(
    ProtectionPurchaseParams calldata _protectionPurchaseParams,
    uint256 _maxPremiumAmount
  ) external override whenNotPaused nonReentrant {
    /// Verify that user can buy protection and then create protection
    _verifyAndCreateProtection(
      block.timestamp,
      _protectionPurchaseParams,
      _maxPremiumAmount,
      false
    );
  }

function _verifyAndCreateProtection(
    uint256 _protectionStartTimestamp,
    ProtectionPurchaseParams calldata _protectionPurchaseParams,
    uint256 _maxPremiumAmount,
    bool _isRenewal
  ) internal {
    /// Verify that user can buy protection
    ProtectionPoolHelper.verifyProtection(
      poolCycleManager,
      defaultStateManager,
      address(this),
      poolInfo,
      _protectionStartTimestamp,
      _protectionPurchaseParams,
      _isRenewal
    );

    /// Step 1: Calculate & check the leverage ratio
    /// Ensure that leverage ratio floor is never breached
    totalProtection += _protectionPurchaseParams.protectionAmount;
    uint256 _leverageRatio = calculateLeverageRatio();
    if (_leverageRatio < poolInfo.params.leverageRatioFloor) {
      revert ProtectionPoolLeverageRatioTooLow(_leverageRatio);
    }

    /// Retrieve the lending pool detail and
    /// update the total protection of the lending pool by the protection amount
    LendingPoolDetail storage lendingPoolDetail = lendingPoolDetails[
      _protectionPurchaseParams.lendingPoolAddress
    ];
    lendingPoolDetail.totalProtection += _protectionPurchaseParams
      .protectionAmount;

    /// Step 2: Calculate the protection premium amount scaled to 18 decimals and
    /// scale it down to the underlying token decimals.
    (
      uint256 _premiumAmountIn18Decimals,
      uint256 _premiumAmount,
      bool _isMinPremium
    ) = ProtectionPoolHelper.calculateAndTrackPremium(
        premiumCalculator,
        protectionBuyerAccounts,
        poolInfo,
        lendingPoolDetail,
        _protectionPurchaseParams,
        _maxPremiumAmount,
        totalSTokenUnderlying,
        _leverageRatio
      );
    totalPremium += _premiumAmount;

    /// Step 3: Calculate protection in days and scale it to 18 decimals.
    uint256 _protectionDurationInDaysScaled = ((
      _protectionPurchaseParams.protectionDurationInSeconds
    ) * Constants.SCALE_18_DECIMALS) / uint256(Constants.SECONDS_IN_DAY);

    console.log(
      "protectionDurationInDays: %s, protectionPremium: %s, leverageRatio: ",
      _protectionDurationInDaysScaled,
      _premiumAmount,
      _leverageRatio
    );

    /// Step 4: Capture loan protection data for premium accrual calculation
    // solhint-disable-next-line
    (int256 _k, int256 _lambda) = AccruedPremiumCalculator.calculateKAndLambda(
      _premiumAmountIn18Decimals,
      _protectionDurationInDaysScaled,
      _leverageRatio,
      poolInfo.params.leverageRatioFloor,
      poolInfo.params.leverageRatioCeiling,
      poolInfo.params.leverageRatioBuffer,
      poolInfo.params.curvature,
      _isMinPremium ? poolInfo.params.minCarapaceRiskPremiumPercent : 0
    );

    /// Step 5: Add protection to the pool
    protectionInfos.push(
      ProtectionInfo({
        buyer: msg.sender,
        protectionPremium: _premiumAmount,
        startTimestamp: _protectionStartTimestamp,
        K: _k,
        lambda: _lambda,
        expired: false,
        purchaseParams: _protectionPurchaseParams
      })
    );

    /// Step 6: Track all loan protections for a lending pool to calculate
    // the total locked amount for the lending pool, when/if pool is late for payment
    uint256 _protectionIndex = protectionInfos.length - 1;
    lendingPoolDetail.activeProtectionIndexes.add(_protectionIndex);
    protectionBuyerAccounts[msg.sender].activeProtectionIndexes.add(
      _protectionIndex
    );

    emit ProtectionBought(
      msg.sender,
      _protectionPurchaseParams.lendingPoolAddress,
      _protectionPurchaseParams.protectionAmount,
      _premiumAmount
    );

    /// Step 7: transfer premium amount from buyer to pool
    poolInfo.underlyingToken.(
      msg.sender,
      address(this),
      _premiumAmount
    );
  }
```

## Impact
A buyer can bypass the protection amount limit check by buying more protections for the same position for a given lending pool. As a result, the lender might be able to claim more payout than the total amount that he lent, that is, he will get more payment than he has lost. 


## Code Snippet

## Tool used
VScode

Manual Review

## Recommendation
Make sure that a buyer can only have one protection for each position in a lending pool, although he can renew the protection when it expires. 