bin2chen

medium

# withdraw() probably get much less than expected

## Summary

withdraw() Missing slippage parameter may cause users to lose shares

## Vulnerability Detail

Users can use withdraw() to retrieve their own funds, the method will calculate how much Underlying token can be retrieved from the corresponding shares based on the exchange rate
```solidity
  function withdraw(uint256 _sTokenWithdrawalAmount, address _receiver)
    external
    override
    whenPoolIsOpen
    whenNotPaused
    nonReentrant
  {
    /// Step 1: Retrieve withdrawal details for current pool cycle index
    uint256 _currentCycleIndex = poolCycleManager.getCurrentCycleIndex(
      address(this)
    );
    WithdrawalCycleDetail storage withdrawalCycle = withdrawalCycleDetails[
      _currentCycleIndex
    ];

    /// Step 2: Verify withdrawal request exists in this withdrawal cycle for the user
    uint256 _sTokenRequested = withdrawalCycle.withdrawalRequests[msg.sender];
    if (_sTokenRequested == 0) {
      revert NoWithdrawalRequested(msg.sender, _currentCycleIndex);
    }

    /// Step 3: Verify that withdrawal amount is not more than the requested amount.
    if (_sTokenWithdrawalAmount > _sTokenRequested) {
      revert WithdrawalHigherThanRequested(msg.sender, _sTokenRequested);
    }

    /// Step 4: calculate underlying amount to transfer based on sToken withdrawal amount
    uint256 _underlyingAmountToTransfer = convertToUnderlying(
      _sTokenWithdrawalAmount
    );

    /// Step 5: burn sTokens shares.
    /// This step must be done after calculating underlying amount to be transferred
    _burn(msg.sender, _sTokenWithdrawalAmount);

    /// Step 6: Update total sToken underlying amount
    totalSTokenUnderlying -= _underlyingAmountToTransfer;

    /// Step 7: update seller's withdrawal amount and total requested withdrawal amount
    withdrawalCycle.withdrawalRequests[msg.sender] -= _sTokenWithdrawalAmount;
    withdrawalCycle.totalSTokenRequested -= _sTokenWithdrawalAmount;

    /// Step 8: transfer underlying token to receiver
    poolInfo.underlyingToken.safeTransfer(
      _receiver,
      _underlyingAmountToTransfer
    );

    emit WithdrawalMade(msg.sender, _sTokenWithdrawalAmount, _receiver);
  }

```

There is a problem here, that there may be have a big slippage

If the bot executes DefaultStateManager.assessStates() before withdraw() (all in memory pool)

assessStates() -> lockCapital() , it will only reduce the totalSTokenUnderlying, but not reduce the totalSupply(), which will cause the exchange rate to become dramatically lower.

```solidity
  function lockCapital(address _lendingPoolAddress)
    external
    payable
    override
    onlyDefaultStateManager
    whenNotPaused
    returns (uint256 _lockedAmount, uint256 _snapshotId)
  {
...
    unchecked {
      /// step 3: Update total locked & available capital in storage
      if (totalSTokenUnderlying < _lockedAmount) {
        /// If totalSTokenUnderlying < _lockedAmount, then lock all available capital
        _lockedAmount = totalSTokenUnderlying;
        totalSTokenUnderlying = 0;            //<---------@audit change totalSTokenUnderlying
      } else {
        /// Reduce the total sToken underlying amount by the locked amount
        totalSTokenUnderlying -= _lockedAmount;  //<---------@audit change totalSTokenUnderlying
      }
    }
```

if totalSTokenUnderlying be change to 0 and  then _underlyingAmountToTransfer will equal 0

This way the user consumes the shares and gets nothing(or less), but it is still possible to recover Accrued Premium to totalSTokenUnderlying

 So it is recommended to add the parameter minUnderlyingAmount

if _underlyingAmountToTransfer< minUnderlyingAmount will revert

## Impact

the user may lose shares

## Code Snippet

https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/pool/ProtectionPool.sol#L226-L275

## Tool used

Manual Review

## Recommendation

```solidity
-  function withdraw(uint256 _sTokenWithdrawalAmount, address _receiver)
+  function withdraw(uint256 _sTokenWithdrawalAmount, uint256 minUnderlyingAmount, address _receiver)
    external
    override
    whenPoolIsOpen
    whenNotPaused
    nonReentrant
  {
...
    uint256 _underlyingAmountToTransfer = convertToUnderlying(
      _sTokenWithdrawalAmount
    );

+   require(_underlyingAmountToTransfer>=minUnderlyingAmount,"bad underlying Amount");

```
