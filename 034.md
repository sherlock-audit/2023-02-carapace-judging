bin2chen

high

# _assessState() may be in Late state forever, capital cannot be unlocked

## Summary

## Vulnerability Detail
In the current protocol, robot will periodically call method DefaultStateManager.assessStates to change the lendingPool state, while executing such actions as lock capital or unlock , etc.

The logic for the state transfer is in _assessState() 
it will change Status based on the previous Status of _lendingPool and the current Status of _lendingPool 
 the following code:
```solidity
  function _assessState(ProtectionPoolState storage poolState) internal {
    poolState.updatedTimestamp = block.timestamp;

    /// assess the state of all reference lending pools of this protection pool
    (
      address[] memory _lendingPools,
      LendingPoolStatus[] memory _currentStatuses
    ) = poolState
        .protectionPool
        .getPoolInfo()
        .referenceLendingPools
        .assessState();

    /// Compare previous and current status of each lending pool and perform the required state transition
    uint256 _length = _lendingPools.length;
    for (uint256 _lendingPoolIndex; _lendingPoolIndex < _length; ) {
      /// Get the lending pool state details
      address _lendingPool = _lendingPools[_lendingPoolIndex];
      LendingPoolStatusDetail storage lendingPoolStateDetail = poolState
        .lendingPoolStateDetails[_lendingPool];

      /// Get the previous and current status of the lending pool
      LendingPoolStatus _previousStatus = lendingPoolStateDetail.currentStatus;
      LendingPoolStatus _currentStatus = _currentStatuses[_lendingPoolIndex];

      if (_previousStatus != _currentStatus) {
        console.log(
          "DefaultStateManager: Lending pool %s status is changed from %s to  %s",
          _lendingPool,
          uint256(_previousStatus),
          uint256(_currentStatus)
        );
      }

      /// State transition 1: Active or LateWithinGracePeriod -> Late
      if (
        (_previousStatus == LendingPoolStatus.Active ||
          _previousStatus == LendingPoolStatus.LateWithinGracePeriod) &&
        _currentStatus == LendingPoolStatus.Late
      ) {
        /// Update the current status of the lending pool to Late
        /// and move the lending pool to the locked state
        lendingPoolStateDetail.currentStatus = LendingPoolStatus.Late;
        _moveFromActiveToLockedState(poolState, _lendingPool);

        /// Capture the timestamp when the lending pool became late
        lendingPoolStateDetail.lateTimestamp = block.timestamp;
      } else if (_previousStatus == LendingPoolStatus.Late) {
        /// Once there is a late payment, we wait for 2 payment periods.
        /// After 2 payment periods are elapsed, either full payment is going to be made or not.
        /// If all missed payments(full payment) are made, then a pool goes back to active.
        /// If full payment is not made, then this lending pool is in the default state.
        if (
          block.timestamp >
          (lendingPoolStateDetail.lateTimestamp +
            _getTwoPaymentPeriodsInSeconds(poolState, _lendingPool))
        ) {
          /// State transition 2: Late -> Active
          if (_currentStatus == LendingPoolStatus.Active) {
            /// Update the current status of the lending pool to Active
            /// and move the lending pool to the active state
            lendingPoolStateDetail.currentStatus = LendingPoolStatus.Active;
            _moveFromLockedToActiveState(poolState, _lendingPool);

            /// Clear the late timestamp
            lendingPoolStateDetail.lateTimestamp = 0;
          }
          /// State transition 3: Late -> Defaulted
          else if (_currentStatus == LendingPoolStatus.Late) {
            /// Update the current status of the lending pool to Active
            lendingPoolStateDetail.currentStatus = LendingPoolStatus.Defaulted;

            // Default state transition will be implemented in the next version of the protocol
            // _moveFromLockedToDefaultedState(poolState, _lendingPool);
          }
//<------------@ if _currentStatus == LendingPoolStatus.Expired ?????? 
        }
      } else if (
        _previousStatus == LendingPoolStatus.Defaulted ||
        _previousStatus == LendingPoolStatus.Expired
      ) {
        /// no state transition for Defaulted or Expired state
      } else {
        /// Only update the status in storage if it is changed
        if (_previousStatus != _currentStatus) {
          lendingPoolStateDetail.currentStatus = _currentStatus;
          /// No action required for any other state transition
        }
      }

      unchecked {
        ++_lendingPoolIndex;
      }
    }
  }
```
Simplify the above code, We mainly focus on what happens if the previous state is Late:
```solidity
     } else if (_previousStatus == LendingPoolStatus.Late) {
        if (
          block.timestamp >
          (lendingPoolStateDetail.lateTimestamp +
            _getTwoPaymentPeriodsInSeconds(poolState, _lendingPool))
        ) {
          /// State transition 2: Late -> Active
          if (_currentStatus == LendingPoolStatus.Active) {
            /// Update the current status of the lending pool to Active
            /// and move the lending pool to the active state
            lendingPoolStateDetail.currentStatus = LendingPoolStatus.Active;
            _moveFromLockedToActiveState(poolState, _lendingPool);

            /// Clear the late timestamp
            lendingPoolStateDetail.lateTimestamp = 0;
          }
          /// State transition 3: Late -> Defaulted
          else if (_currentStatus == LendingPoolStatus.Late) {
            /// Update the current status of the lending pool to Active
            lendingPoolStateDetail.currentStatus = LendingPoolStatus.Defaulted;

            // Default state transition will be implemented in the next version of the protocol
            // _moveFromLockedToDefaultedState(poolState, _lendingPool);
          }
      
        }
      } else if (
        _previousStatus == LendingPoolStatus.Defaulted ||
        _previousStatus == LendingPoolStatus.Expired
      ) {
```

We can see that if the previous status is Late (and TwoPaymentPeriods later), there are only two cases:
1._currentStatus == Active , Late -> Active
2._currentStatus == Late , Late -> Defaulted

There is a problem here, what if the current status becomes expired? Here it will be ignored
3._currentStatus==Expired,    ignored  (still Late)

 Since this judgment is made after two payment dates, it is very likely that Goldfinch paid in full and the lendingPool expired during this time
What's worse is that if the current state becomes expired, it will only ever be expired

 If this happens, then the status will always stay at Late.

 Since it is currently Late, it already has LockCapital, this LockCapital will always lock, and can't change to unlock

Funds are always left in the contract


## Impact

LockCapital  can't  unlock,  funds are always left in the contract

## Code Snippet

https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/DefaultStateManager.sol#L336-L365

## Tool used

Manual Review

## Recommendation

```solidity
...
     } else if (_previousStatus == LendingPoolStatus.Late) {
        if (
          block.timestamp >
          (lendingPoolStateDetail.lateTimestamp +
            _getTwoPaymentPeriodsInSeconds(poolState, _lendingPool))
        ) {
          /// State transition 2: Late -> Active
-         if (_currentStatus == LendingPoolStatus.Active) {
+         if (_currentStatus == LendingPoolStatus.Active || _currentStatus == LendingPoolStatus.Expired) {
            /// Update the current status of the lending pool to Active
            /// and move the lending pool to the active state
            lendingPoolStateDetail.currentStatus = LendingPoolStatus.Active;
            _moveFromLockedToActiveState(poolState, _lendingPool);

            /// Clear the late timestamp
            lendingPoolStateDetail.lateTimestamp = 0;
          }
          /// State transition 3: Late -> Defaulted
          else if (_currentStatus == LendingPoolStatus.Late) {
            /// Update the current status of the lending pool to Active
            lendingPoolStateDetail.currentStatus = LendingPoolStatus.Defaulted;

            // Default state transition will be implemented in the next version of the protocol
            // _moveFromLockedToDefaultedState(poolState, _lendingPool);
          }
        }
      } else if (
```

