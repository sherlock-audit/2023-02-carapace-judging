XKET

medium

# `DefaultStateManager._assessState()` should change the status from `Late` to `Defaulted` if the last status is `Expired`.



## Summary
If `DefaultStateManager._assessState()` meets the `Late` status, it changes the status like `Late => Active` or `Late => Defaulted` after waiting for 2 * `PaymentPeriod`.

But as I can check from the test, the status of the lending pool might be changed to `Expired` and the status will be stuck without changing to `Active` or `Defaulted`.

## Vulnerability Detail
`DefaultStateManager._assessState()` checks like below after meeting the first `Late` status.

```solidity
File: DefaultStateManager.sol
336:       } else if (_previousStatus == LendingPoolStatus.Late) {
337:         /// Once there is a late payment, we wait for 2 payment periods.
338:         /// After 2 payment periods are elapsed, either full payment is going to be made or not.
339:         /// If all missed payments(full payment) are made, then a pool goes back to active.
340:         /// If full payment is not made, then this lending pool is in the default state.
341:         if (
342:           block.timestamp > //@audit-info 2 * paymentPeriod passed already
343:           (lendingPoolStateDetail.lateTimestamp +
344:             _getTwoPaymentPeriodsInSeconds(poolState, _lendingPool))
345:         ) {
346:           /// State transition 2: Late -> Active
347:           if (_currentStatus == LendingPoolStatus.Active) {
348:             /// Update the current status of the lending pool to Active
349:             /// and move the lending pool to the active state
350:             lendingPoolStateDetail.currentStatus = LendingPoolStatus.Active;
351:             _moveFromLockedToActiveState(poolState, _lendingPool);active
352: 
353:             /// Clear the late timestamp
354:             lendingPoolStateDetail.lateTimestamp = 0;
355:           }
356:           /// State transition 3: Late -> Defaulted
357:           else if (_currentStatus == LendingPoolStatus.Late) {
358:             /// Update the current status of the lending pool to Active
359:             lendingPoolStateDetail.currentStatus = LendingPoolStatus.Defaulted;
360: 
361:             // Default state transition will be implemented in the next version of the protocol
362:             // _moveFromLockedToDefaultedState(poolState, _lendingPool);
363:           } //@audit how to manage if expired directly?
364:         }
365:       }
```

It changes the status to `Active` or `Defaulted` only if the pool's status is `Active` or `Late` after waiting for 2 payment periods.

And from the below test code, we can see it's possible to meet an `Expired` status from the lending pool.

```solidity
File: DefaultStateManager.test.ts
491:     // TODO: Discuss with goldfinch team
492:     // Why does lending pool not accept large payments?
493:     xdescribe("Active -> Expired", async () => {
494:       before(async () => {
495:         // pay 10M to 2nd lending pool to move it to Expired state
496:         await moveForwardTimeByDays(30);
497:         await payToLendingPoolAddress(lendingPools[1], "3000000", usdcContract);
498: 
499:         await defaultStateManager.assessStateBatch([pool1]);
500:       });
501: 
502:       it("...lending pool 2 should be in Expired state", async () => {
503:         expect(
504:           await defaultStateManager.getLendingPoolStatus(
505:             pool1,
506:             lendingPool2.address
507:           )
508:         ).to.eq(5);
509:       });
510:     });
```

So in this case, the status will be `Late` forever and the locked capital will be stuck also.(rather than claimed by sellers or defaulted by buyers.)

## Impact
If the lending pool meets the `Expired` status after the `Late` status, the locked capital will be stuck forever.

## Code Snippet
https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/DefaultStateManager.sol#L341-L363

## Tool used
Manual Review

## Recommendation
I think we should change the status to `Defaulted` if the pool's status is not `Active` after waiting for 2 payment periods.

```solidity
        if (
          block.timestamp >
          (lendingPoolStateDetail.lateTimestamp +
            _getTwoPaymentPeriodsInSeconds(poolState, _lendingPool))
        ) {
          /// State transition 2: Late -> Active
          if (_currentStatus == LendingPoolStatus.Active) {
            /// Update the current status of the lending pool to Active
            /// and move the lending pool to the active state
            lendingPoolStateDetail.currentStatus = LendingPoolStatus.Active;
            _moveFromLockedToActiveState(poolState, _lendingPool);

            /// Clear the late timestamp
            lendingPoolStateDetail.lateTimestamp = 0;
          }
          /// State transition 3: Late -> Defaulted
          else { //@audit don't chek any conditions
            /// Update the current status of the lending pool to Defaulted
            lendingPoolStateDetail.currentStatus = LendingPoolStatus.Defaulted;

            // Default state transition will be implemented in the next version of the protocol
            // _moveFromLockedToDefaultedState(poolState, _lendingPool);
          }
        }
      } 
```