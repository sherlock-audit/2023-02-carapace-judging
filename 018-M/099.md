VAD37

medium

# `ProtectionPoolCycleManager` does not support change state from Open to Open

## Summary

`ProtectionPoolCycleManager.calculateAndSetPoolCycleState()` flip state from Open -> Locked and Locked -> Open based on blocktime and duration.
The function comment said
`
Determines & sets the current cycle state of the given pool.
This function also starts a new cycle if required.
`
Yet the function not start new cycle right away when total duration have been passed but required to call twice to change state from Open -> Locked -> new cycle
## Vulnerability Detail

In case of no cron job or no user action during entire pool cycle (~10 days) or locked cycle really short (same as no locked cycle).

Cycle 1 go to Cycle 2 without go into locked state. The current time is in Cycle 2, and it should be in Open state.

Then `calculateAndSetPoolCycleState()` would need to be called twice to change state Open -> Locked -> Open.

Most of `ProtectionPool.sol` function only call `calculateAndSetPoolCycleState()` once then verify if poolCycle is Open. Which it will flip cycle into Locked state, when it should be Open state.

## Impact

Most User action will be reverted if Cycle go through Locked duration without changing to Locked state. (Ex: `buyProtection()`)

## Code Snippet

https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/ProtectionPoolCycleManager.sol#L114-L140

`ProtectionPoolCycleManager` logic flip state when the function name and doc said it sets current cycle and start new cycle if required.

```js
File: contracts\core\ProtectionPoolCycleManager.sol
114:     /// If cycle is open, then check if it's time to move to LOCKED state.
115:     if (currentState == ProtectionPoolCycleState.Open) {
116:       /// If current time is past the initial open duration, then move to LOCKED state.
117:       if (
118:         block.timestamp - poolCycle.currentCycleStartTime >
119:         poolCycle.params.openCycleDuration
120:       ) {
121:         poolCycle.currentCycleState = _newState = ProtectionPoolCycleState
122:           .Locked;
123:       } 
            //// Missing check if entire cycle already passed and it should start new cycle here
124:     }
125:     /// If cycle is locked, then check if it's time to start a new cycle.
126:     else if (currentState == ProtectionPoolCycleState.Locked) {
127:       /// If current time is past the total cycle duration, then start a new cycle.
128:       if (
129:         block.timestamp - poolCycle.currentCycleStartTime >
130:         poolCycle.params.cycleDuration
131:       ) {
132:         /// move current cycle to a new cycle
133:         _startNewCycle(
134:           _protectionPoolAddress,
135:           poolCycle,
136:           poolCycle.currentCycleIndex + 1
137:         );
138:         _newState = ProtectionPoolCycleState.Open;
139:       }
140:     }

```

Revert call Example: [`ProtectionPoolHelper.verifyProtection()`](https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/pool/ProtectionPool.sol#L802) **reverted** when going through [`_verifyProtectionDuration()`](https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/libraries/ProtectionPoolHelper.sol#L462) and [`_verifyLendingPoolIsActive()`](https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/libraries/ProtectionPoolHelper.sol#L63)


[`_verifyProtectionDuration()` call `calculateAndSetPoolCycleState()` once](https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/libraries/ProtectionPoolHelper.sol#L462) to flip state from Open to Locked. (Assuming it should be Open, call calculate twice would start new cycle)

The next call `_verifyLendingPoolIsActive()` will simply revert

## Tool used

Manual Review

## Recommendation

`calculateAndSetPoolCycleState` go into Locked State does not impact other class. The only thing it does is start a new cycle.

It should do so first in if-else case

```js
File: contracts\core\ProtectionPoolCycleManager.sol

  function calculateAndSetPoolCycleState(address _protectionPoolAddress)
    external
    override
    returns (ProtectionPoolCycleState _newState) //@audit calculatePoolcycle switch/flip state. Could go from open to locked, while it should be open.
  {
    ProtectionPoolCycle storage poolCycle = protectionPoolCycles[
      _protectionPoolAddress
    ];

    /// Gas optimization:
    /// Store the current cycle state in memory instead of reading it from the storage multiple times.
    ProtectionPoolCycleState currentState = _newState = poolCycle
      .currentCycleState;

    /// If cycle is not started, that means pool is NOT registered yet.
    /// So, we can't move the cycle state
    if (currentState == ProtectionPoolCycleState.None) {
      return _newState;
    }

    ///Changed: Move locked state logic outside. Or move this into if-Open case
    if (
      block.timestamp - poolCycle.currentCycleStartTime >
      poolCycle.params.cycleDuration
    ) {
      /// move current cycle to a new cycle
      _startNewCycle(
        _protectionPoolAddress,
        poolCycle,
        poolCycle.currentCycleIndex + 1
      );
      _newState = ProtectionPoolCycleState.Open;
    }
    /// If cycle is open, then check if it's time to move to LOCKED state.
    else if (currentState == ProtectionPoolCycleState.Open) {
      /// If current time is past the initial open duration, then move to LOCKED state.
      if (
        block.timestamp - poolCycle.currentCycleStartTime >
        poolCycle.params.openCycleDuration
      ) {
        poolCycle.currentCycleState = _newState = ProtectionPoolCycleState
          .Locked;
      }
    }
    /// The last else case never reached so below code can be removed.
    /// If cycle is locked, then check if it's time to start a new cycle.
    else if (currentState == ProtectionPoolCycleState.Locked) {
      /// If current time is past the total cycle duration, then start a new cycle.
      if (
        block.timestamp - poolCycle.currentCycleStartTime >
        poolCycle.params.cycleDuration
      ) {
        /// move current cycle to a new cycle
        _startNewCycle(
          _protectionPoolAddress,
          poolCycle,
          poolCycle.currentCycleIndex + 1
        );
        _newState = ProtectionPoolCycleState.Open;
      }
    }

    return _newState;
  }
```
