HonorLt

medium

# Max limit of lending pools

## Summary

The lending pool array is susceptible to the infamous loop gas limit issue.

## Vulnerability Detail

`lendingPools` have no upper limit. It pushes the new entry to the dynamic array:

```solidity
  /// @notice an array of all the added lending pools in this basket
  address[] private lendingPools;
```

```solidity
  function addReferenceLendingPool(
    address _lendingPoolAddress,
    LendingProtocol _lendingPoolProtocol,
    uint256 _protectionPurchaseLimitInDays
  ) external payable onlyOwner {
    _addReferenceLendingPool(
      _lendingPoolAddress,
      _lendingPoolProtocol,
      _protectionPurchaseLimitInDays
    );
  }
```

```solidity
  function _addReferenceLendingPool(
    address _lendingPoolAddress,
    LendingProtocol _lendingPoolProtocol,
    uint256 _protectionPurchaseLimitInDays
  ) internal {
    ...

    lendingPools.push(_lendingPoolAddress);

    ...
  }
```

So the `lendingPools` is an unbounded array. The problem is that when assessing the state, it needs to iterate over all the pools and get their statuses:

```solidity
  function assessState()
    public
    view
    override
    returns (
      address[] memory _lendingPools,
      LendingPoolStatus[] memory _statuses
    )
  {
    uint256 _length = lendingPools.length;
    _lendingPools = new address[](_length);
    _statuses = new LendingPoolStatus[](_length);

    /// Iterate through all the lending pools in this basket and get their statuses
    for (uint256 i; i < _length; ) {
      _lendingPools[i] = lendingPools[i];
      _statuses[i] = _getLendingPoolStatus(lendingPools[i]);

      unchecked {
        ++i;
      }
    }
  }
```

`DefaultStateManager` utilizes this function and then iterates over the whole array once again:
```solidity
  function _assessState(ProtectionPoolState storage poolState) internal {
    poolState.updatedTimestamp = block.timestamp;

    /// assess the state of all reference lending pools of this protection pool
    (
      address[] memory _lendingPools,
      LendingPoolStatus[] memory _currentStatuses
    ) = poolState
        .protectionPool
        .getPoolInfo()
        .referenceLendingPools
        .assessState();

    /// Compare previous and current status of each lending pool and perform the required state transition
    uint256 _length = _lendingPools.length;
    for (uint256 _lendingPoolIndex; _lendingPoolIndex < _length; )
    ...
```

Over time the protocol will grow and new lending pools will be added. Once the limit is hit the executions of such functions will revert. The protocol is intended to be deployed on mainnet which currently has a block gas limit of around 30M.

## Impact

If the array of lending pools grows too large to fit in the block, the state assessing will fail and leave the protocol inoperable. 
There is no way to remove or suspend the lending pool once added unless the contract is upgraded.

## Code Snippet

https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/pool/ReferenceLendingPools.sol#L35

https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/pool/ReferenceLendingPools.sol#L281

https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/pool/ReferenceLendingPools.sol#L184-L206

https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/DefaultStateManager.sol#L289-L304

## Tool used

Manual Review

## Recommendation

Consider introducing upper boundary values for the dynamic arrays, that can be adjusted based on deployed environment and its limitations.
