Koolex

high

# Missing validation of termEndTime of Goldfinch's lending pools

## Summary
Missing validation of termEndTime of Goldfinch's lending pools

## Vulnerability Detail
When creating a protection pool, the owner should pass the reference lending pools. The protection pool then gets registered to the **pool cycle manager** and **default state manager**.

```sh
 /// register newly created protection pool to the pool cycle manager
    protectionPoolCycleManager.registerProtectionPool(
      _poolProxyAddress,
      _poolCycleParams
    );

    /// register newly created protection pool to the default state manager
    defaultStateManager.registerProtectionPool(_poolProxyAddress);
```

On `defaultStateManager.registerProtectionPool`, the **default state manager** assesses the state of the newly registered protection pool by assessing the state of all its reference lending pools.

```sh
/// Assess the state of the newly registered protection pool
    _assessState(poolState);
	
    emit ProtectionPoolRegistered(_protectionPoolAddress);
```

**DefaultStateManager** calls `ReferenceLendingPools.assessState`, then **ReferenceLendingPools** get the status of a lending pool as follows:
It checks the status by calling multiple functions of **GoldfinchAdapter** to return one of the following statuses:
1. LendingPoolStatus.**Expired**
2. LendingPoolStatus.**LateWithinGracePeriod**
3. LendingPoolStatus.**Late**

if all above statuses are invalid then it returns LendingPoolStatus.**Active**.

Obviously for LendingPoolStatus.**Expired** to be returned, `GoldfinchAdapter.isLendingPoolExpired` must return true.

Now if we check the function `GoldfinchAdapter.isLendingPoolExpired` , we notice that it returns true if the termEndTime is zero since block.timestamp is always greater than zero.

```sh
    return
      block.timestamp >= _termEndTimestamp ||
      (_termEndTimestamp > 0 && _creditLine.balance() == 0);
```


In Goldfinch, the **termEndTime** is only set upon the first drawdown. **otherwise, it is zero**. 

```sh
 uint256 timestamp = currentTime();
    require(termEndTime == 0 || (timestamp < termEndTime), "After termEndTime");
    require(amount.add(balance) <= currentLimit, "Cannot drawdown more than the limit");
    require(amount > 0, "Invalid drawdown amount");

    if (balance == 0) {
      setInterestAccruedAsOf(timestamp);
      setLastFullPaymentTime(timestamp);
      setTotalInterestAccrued(0);
      // Set termEndTime only once to prevent extending
      // the loan's end time on every 0 balance drawdown
      if (termEndTime == 0) {
        setTermEndTime(timestamp.add(SECONDS_PER_DAY.mul(termInDays)));
      }
    }
```

And according to Goldfinch docs
> the Borrower can lock the pool and drawdown at any time

https://docs.goldfinch.finance/goldfinch/guides/participating-in-borrower-pools#is-it-necessary-for-the-backer-portion-of-the-pool-to-be-filled-before-the-borrower-locks-the-pool-a

This means any lending pool will be marked as expired in Carapace protocol if the borrower (in Goldfinch) didn't make the first drawdown (which will lock the pool as well) yet.

## Impact
- Upon registering a protection pool and assessing the state of its lending pools, some of the lending pools **get expired immediately** in Carapace protocol and they will never be active again. 
- The lending pools are **unusable** and protection buyers **can not buy protection** coverage for them.
- It is possible that those expired lending pools go into **LendingPoolStatus.Late** status and then to **LendingPoolStatus.Active** again. After that, protection buyers can buy protection. However, they **missed covering the risk of loan defaulting** which is the purpose of buying protection.
- **You can not add those lending pools to a different protection** pool as they will also get expired immediately upon registration if termEndTime is still zero.
- If those lending pools get added later to a different protection pool at a time where termEndTime is not zero any longer, then you would have **inconsistent lending pool status** among different protection pools.



## Code Snippet
### Carapace
- Assessing the state of the newly registered protection pool
https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/DefaultStateManager.sol#L113

- Assessing a lending pool state
https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/pool/ReferenceLendingPools.sol#L184

- GoldfinchAdapter.isLendingPoolExpired
https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/adapters/GoldfinchAdapter.sol#L74-L76


### Goldfinch

- TranchedPool.drawdown
```sh
  function drawdown(uint256 amount) external override onlyLocker whenNotPaused {
    /// @dev DP: drawdowns paused
    require(!drawdownsPaused, "DP");
    if (!_locked()) {
      // Assumes the senior pool has invested already (saves the borrower a separate transaction to lock the pool)
      _lockPool();
    }
    // Drawdown only draws down from the current slice for simplicity. It's harder to account for how much
    // money is available from previous slices since depositors can redeem after unlock.
    PoolSlice storage currentSlice = _poolSlices[numSlices - 1];
    uint256 amountAvailable = TranchingLogic.sharePriceToUsdc(
      currentSlice.juniorTranche.principalSharePrice,
      currentSlice.juniorTranche.principalDeposited
    );
    amountAvailable = amountAvailable.add(
      TranchingLogic.sharePriceToUsdc(
        currentSlice.seniorTranche.principalSharePrice,
        currentSlice.seniorTranche.principalDeposited
      )
    );

    /// @dev IF: insufficient funds
    require(amount <= amountAvailable, "IF");

    creditLine.drawdown(amount);
	.
	.
	.
	
```

https://github.com/goldfinch-eng/mono/blob/bd9adae6fbd810d1ebb5f7ef22df5bb6f1eaee3b/packages/protocol/contracts/protocol/core/TranchedPool.sol#L212-L236

- CreditLine.drawdown
```sh
function drawdown(uint256 amount) external onlyAdmin {
    uint256 timestamp = currentTime();
    require(termEndTime == 0 || (timestamp < termEndTime), "After termEndTime");
    require(amount.add(balance) <= currentLimit, "Cannot drawdown more than the limit");
    require(amount > 0, "Invalid drawdown amount");

    if (balance == 0) {
      setInterestAccruedAsOf(timestamp);
      setLastFullPaymentTime(timestamp);
      setTotalInterestAccrued(0);
      // Set termEndTime only once to prevent extending
      // the loan's end time on every 0 balance drawdown
      if (termEndTime == 0) {
        setTermEndTime(timestamp.add(SECONDS_PER_DAY.mul(termInDays)));
      }
    }
	.
	.
	.
	
```
https://github.com/goldfinch-eng/mono/blob/bd9adae6fbd810d1ebb5f7ef22df5bb6f1eaee3b/packages/protocol/contracts/protocol/core/CreditLine.sol#L90-L105
## Tool used

Manual Review

## Recommendation
One possibility is to disallow including a lending pool upon protection pool registeration if the termEndTime is zero.
Otherwise, avoid marking the lending pool as expired if the termEndTime is zero.