Allarious

high

# `calculateAndSetPoolCycleState` does not update the state correctly

## Summary
In cases where `calculateAndSetPoolCycleState` is not called for some time, the state update might happen incorrectly.

## Vulnerability Detail
`calculateAndSetPoolCycleState` first checks to see if the state is in `Open`, in this case, it updates the state to `Locked` if the time difference with `cycleStartTime` is more than the `openCycleDuration`. In this case, if the time is in the next cycle, instead of going to the next cycle it just locks the state.

## Impact
Cycles will not get updated correctly and users can/can't withdraw according to the specifications outlined by the project. Also, the view functions would output wrong data and may cause calculations to be wrong.

(1) Imagine if a cycle duration is 10 days, and the open duration is 6 days
(2) if the time passed from the start of the cycle is 11 days, and the state is not updated since, in case where `calculateAndSetPoolCycleState` is called after 11 days, instead of starting a new cycle it updates the state to locked.
(3) At this point, all the view functions would output wrong data even after `calculateAndSetPoolCycleState` is called.

This problem gets bigger when the distance between `openCycleDuration` and `cycleDuration` are low, and the protocol can not update the state in a timely manner.

https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/ProtectionPoolCycleManager.sol#L94-L143

## Code Snippet

## Tool used

Manual Review

## Recommendation
`calculateAndSetPoolCycleState` should be updated into:
```solidity
  function calculateAndSetPoolCycleState(address _protectionPoolAddress)
    external
    override
    returns (ProtectionPoolCycleState _newState)
  {
    ProtectionPoolCycle storage poolCycle = protectionPoolCycles[
      _protectionPoolAddress
    ];

    /// Gas optimization:
    /// Store the current cycle state in memory instead of reading it from the storage multiple times.
    ProtectionPoolCycleState currentState = _newState = poolCycle
      .currentCycleState;

    /// If cycle is not started, that means pool is NOT registered yet.
    /// So, we can't move the cycle state
    if (currentState == ProtectionPoolCycleState.None) {
      return _newState;
    }

    if (block.timestamp - poolCycle.currentCycleStartTime > 
        poolCycle.params.cycleDuration){
        _startNewCycle(
          _protectionPoolAddress,
          poolCycle,
          poolCycle.currentCycleIndex + 1
        );
        _newState = ProtectionPoolCycleState.Open;
    } else if (currentState == ProtectionPoolCycleState.Open){
        if (
        block.timestamp - poolCycle.currentCycleStartTime >
        poolCycle.params.openCycleDuration
      ) {
        poolCycle.currentCycleState = _newState = ProtectionPoolCycleState
          .Locked;
      }
    }

    return _newState;
  }
```
