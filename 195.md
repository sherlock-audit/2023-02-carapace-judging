libratus

high

# Seller can use same sTokens to request withdrawals for two different cycles

## Summary
Seller can use same sTokens to request withdrawals for two different cycles. This will allow him to deposit protection without default risk several cycles in the future.

## Vulnerability Detail
Seller can create a withdrawal scheduled two cycles in the future. Withdrawals requests are stored separately for each cycle
https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/pool/ProtectionPool.sol#L1075-L1083

This allows a seller to request withdrawals for two different cycles while holding the same sTokens. Seller can do the following:
- Deposit protection during cycle 1
- Request withdrawal for cycle 3
- Wait until cycle 2
- Request withdrawal for cycle 4
- Wait until cycle 3 and finish withdrawal

The seller managed to create an extra withdrawal for cycle 4. As a result, during cycle 4 they will be able to make deposits from address1 and withdraw during the same cycle. This will allow the seller to collect premiums without risk of default as they will be able to front-run locking transactions and withdraw funds.

## Impact
Locking mechanism can be gamed. Sellers will be able to earn premium without default risk

## Code Snippet
https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/pool/ProtectionPool.sol#L1075-L1083

## Tool used

Manual Review

## Recommendation
When creating a withdrawal for cycle with index `current+2`, check if there is already a withdrawal for cycle `current+1`. Delete this withdrawal
```solidity
  function _requestWithdrawal(uint256 _sTokenAmount) internal {
    uint256 _sTokenBalance = balanceOf(msg.sender);
    if (_sTokenAmount > _sTokenBalance) {
      revert InsufficientSTokenBalance(msg.sender, _sTokenBalance);
    }

    /// Get current cycle index for this pool
    uint256 _currentCycleIndex = poolCycleManager.getCurrentCycleIndex(
      address(this)
    );

    WithdrawalCycleDetail storage nextCycle = withdrawalCycleDetails[
      _currentCycleIndex + 1
    ];

    if (nextCycle.withdrawalRequests[msg.sender] > 0) {
      nextCycle.totalSTokenRequested -= nextCycle.withdrawalRequests[msg.sender];
      nextCycle.withdrawalRequests[msg.sender] = 0;
    }

    /// Actual withdrawal is allowed in open period of cycle after next cycle
    /// For example: if request is made in at some time in cycle 1,
    /// then withdrawal is allowed in open period of cycle 3
    uint256 _withdrawalCycleIndex = _currentCycleIndex + 2;

    WithdrawalCycleDetail storage withdrawalCycle = withdrawalCycleDetails[
      _withdrawalCycleIndex
    ];

    /// Cache existing requested amount for the cycle for the sender
    uint256 _oldRequestAmount = withdrawalCycle.withdrawalRequests[msg.sender];
    withdrawalCycle.withdrawalRequests[msg.sender] = _sTokenAmount;

    unchecked {
      /// Update total requested withdrawal amount for the cycle considering existing requested amount
      if (_oldRequestAmount > _sTokenAmount) {
        withdrawalCycle.totalSTokenRequested -= (_oldRequestAmount -
          _sTokenAmount);
      } else {
        withdrawalCycle.totalSTokenRequested += (_sTokenAmount -
          _oldRequestAmount);
      }
    }

    emit WithdrawalRequested(msg.sender, _sTokenAmount, _withdrawalCycleIndex);
  }
```