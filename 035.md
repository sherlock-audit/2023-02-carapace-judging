bin2chen

high

# accruePremiumAndExpireProtections() protection may never expire and premiumAmount will never be assigned

## Summary

protection may never expire, stay in the active list, and cannot assign premium

## Vulnerability Detail
In the current protocol, robot will periodically call method ProtectionPool.accruePremiumAndExpireProtections to 
accrue premium and expire protection,

```solidity
function _accruePremiumAndExpireProtections(
    LendingPoolDetail storage lendingPoolDetail,
    uint256 _lastPremiumAccrualTimestamp,
    uint256 _latestPaymentTimestamp
  )
    internal
    returns (
      uint256 _accruedPremiumForLendingPool,
      uint256 _totalProtectionRemoved
    )
  {
    /// Get all active protection indexes for the lending pool
    uint256[] memory _protectionIndexes = lendingPoolDetail
      .activeProtectionIndexes
      .values();

    /// Iterate through all active protection indexes for the lending pool
    uint256 _length = _protectionIndexes.length;
    for (uint256 j; j < _length; ) {
      uint256 _protectionIndex = _protectionIndexes[j];
      ProtectionInfo storage protectionInfo = protectionInfos[_protectionIndex];

      /// Verify & accrue premium for the protection and
      /// if the protection is expired, then mark it as expired
      (
        uint256 _accruedPremiumInUnderlying,
        bool _expired
      ) = ProtectionPoolHelper.verifyAndAccruePremium(
          poolInfo,
          protectionInfo,
          _lastPremiumAccrualTimestamp,
          _latestPaymentTimestamp
        );
      _accruedPremiumForLendingPool += _accruedPremiumInUnderlying;

      if (_expired) {
        /// Add removed protection amount to the total protection removed
        _totalProtectionRemoved += protectionInfo
          .purchaseParams
          .protectionAmount;

        ProtectionPoolHelper.expireProtection(
          protectionBuyerAccounts,
          protectionInfo,
          lendingPoolDetail,
          _protectionIndex
        );
        emit ProtectionExpired(
          protectionInfo.buyer,
          protectionInfo.purchaseParams.lendingPoolAddress,
          protectionInfo.purchaseParams.protectionAmount
        );
      }

      unchecked {
        ++j;
      }
    }
  }
```
The above method mainly loops activeProtectionIndexes[] to calculate the current Accrue Premium amount
Remove activeProtectionIndexes if it expired

calculate Accrue Premium amount and whether it expires by method  verifyAndAccruePremium():
 
```solidity
  function verifyAndAccruePremium(
    ProtectionPoolInfo storage poolInfo,
    ProtectionInfo storage protectionInfo,
    uint256 _lastPremiumAccrualTimestamp,
    uint256 _latestPaymentTimestamp
  )
    external
    view
    returns (uint256 _accruedPremiumInUnderlying, bool _protectionExpired)
  {
    uint256 _startTimestamp = protectionInfo.startTimestamp;

    /// This means no payment has been made after the protection is bought or protection starts in the future.
    /// so no premium needs to be accrued.
    if (
      _latestPaymentTimestamp < _startTimestamp ||       //<--------------- if _latestPaymentTimestamp < _startTimestamp still (0,false)
      _startTimestamp > block.timestamp
    ) {
      return (0, false);
    }
.....
```
This method starts by determining that if _latestPaymentTimestamp< _startTimestamp it will always return (_accruedPremiumInUnderlying=0,_protectionExpired=false)

There is a problem with this:
If a protection be added after the _latestPaymentTimestamp, and then the lendingPool is not paid again, it will enter Defaulted and the _latestPaymentTimestamp will remain unchanged, no matter how much time has passed, the protection always return (0,false), so that this is still in  activeProtectionIndexes, and accrue premium is always 0, premiumAmount will never be assigned

 I think this is unreasonable, no matter what the lendingPool status ends up being, this premiumAmount should eventually be assigned （Otherwise it will be locked in the contract）, even in the case of default

So it is recommended to add a limit, if _latestPaymentTimestamp < _startTimestamp, but the expiration time of the protection is already very long, it is considered expired and can accrue premium


## Impact
premiumAmount  maybe locked in the contract
## Code Snippet

https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/core/pool/ProtectionPool.sol#L987-L995

https://github.com/sherlock-audit/2023-02-carapace/blob/main/contracts/libraries/ProtectionPoolHelper.sol#L215-L220

## Tool used

Manual Review

## Recommendation
If the expiration time exceeds 30 days, it will be forced to expire

```solidity
  function verifyAndAccruePremium(
    ProtectionPoolInfo storage poolInfo,
    ProtectionInfo storage protectionInfo,
    uint256 _lastPremiumAccrualTimestamp,
    uint256 _latestPaymentTimestamp
  )
    external
    view
    returns (uint256 _accruedPremiumInUnderlying, bool _protectionExpired)
  {
    uint256 _startTimestamp = protectionInfo.startTimestamp;

    /// This means no payment has been made after the protection is bought or protection starts in the future.
    /// so no premium needs to be accrued.
    if (
-     _latestPaymentTimestamp < _startTimestamp ||
+   (_latestPaymentTimestamp < _startTimestamp && block.timestamp< protectionInfo.startTimestamp +
      protectionInfo.purchaseParams.protectionDurationInSeconds + 30 days ) ||
      _startTimestamp > block.timestamp
    ) {
      return (0, false);
    }
```


